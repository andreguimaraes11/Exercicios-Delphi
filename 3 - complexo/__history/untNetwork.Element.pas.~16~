unit untNetwork.Element;

interface

uses
  System.Generics.Collections;
type
  TNetworkElement = class;

  TNetworkElementList = class
  strict private
    FListaConnections : TObjectList<TNetworkElement>;
  public
    procedure Add(AElement : TNetworkElement);
    procedure Remove(AElement : TNetworkElement); overload;
    procedure Remove(AIdx : integer); overload;

    function Count : integer;

    constructor Create;
    destructor Destroy; override;
  end;

  TNetworkElement = class
  strict private
    FNumber : integer;
    FListaConnections : TNetworkElementList;
  public
    property Number : integer read FNumber;
    property Connections : TNetworkElementList read FListaConnections;
    constructor Create(ANumber : integer);
    destructor Destroy; override;
  end;

implementation

uses
  System.SysUtils, untNetwork.Exceptions;

{ TNetworkElement }

constructor TNetworkElement.Create(ANumber : integer);
begin
  Self.FNumber := ANumber;
  Self.FListaConnections := TNetworkElementList.Create;
end;

destructor TNetworkElement.Destroy;
begin
  if Assigned(FListaConnections) then
    FListaConnections.Free;
  inherited;
end;

{ TNetworkElementList }

procedure TNetworkElementList.Add(AElement: TNetworkElement);
var
  i : integer;
begin
  for i := 0 to pred(FListaConnections.Count) do
  begin
    if AElement.Number = FListaConnections.Items[i].Number then
      raise EElementoJaAdicionado.Create('[TNetworkElementList] - Elemento já adicionado');
  end;

  FListaConnections.Add(AElement);
  FListaConnections.TrimExcess;
end;

function TNetworkElementList.Count: integer;
begin
  Result := FListaConnections.Count;
end;

constructor TNetworkElementList.Create;
begin
  Self.FListaConnections := TObjectList<TNetworkElement>.Create;
  Self.FListaConnections.OwnsObjects := False;
end;

destructor TNetworkElementList.Destroy;
begin
  FListaConnections.Free;
  inherited;
end;

procedure TNetworkElementList.Remove(AElement: TNetworkElement);
begin
  FListaConnections.Remove(AElement);
end;

procedure TNetworkElementList.Remove(AIdx: integer);
begin
  if FListaConnections.Count < AIdx then
    raise Exception.Create('[TNetworkElementList] - Index acessado inválido. Index ['+Aidx.ToString+'] '+
    '- Total da lista ['+FListaConnections.Count.ToString+']');

  FListaConnections.Remove(FListaConnections.Items[AIdx]);
end;

end.
